buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.13.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.20"
        classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:2.2.21"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.57.2"
        classpath "com.google.gms:google-services:4.4.4"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.1.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.53.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: 'com.github.ben-manes.versions'
            apply plugin: 'kotlin-parcelize'

            android {
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                kotlinOptions {
                    jvmTarget = Config.jvmTarget
                }
            }
        }

        // Configure ktlint for Java 21 compatibility
        if (project.plugins.hasPlugin('org.jlleitschuh.gradle.ktlint')) {
            ktlint {
                android = true
                outputToConsole = true
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDir
}